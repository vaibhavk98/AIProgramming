# this was written on Jan 12th, 2016.
# this is the first complex pinecone program ever wretten.
# type the numbers 1, 2, 3 and 4 followed by enter to move.
# because of how early it was wretten, it doesn't use modern constructs, like functions.

w :: 16
h :: 8

meChar :: 79
trailChar :: 43

# init

locX: w/2
locY: h/2

quit: fls

ary: IntArray: w*h

y: 0 | y<h | y: y+1 @
(
	x: 0 | x<w | x: x+1 @
	(
		ary.set: y*w+x, 32
	)
)

# game loop

!quit @
(
	# update
	
	locX<0? locX: 0
	locX>=w? locX: w-1
	locY<0? locY: 0
	locY>=h? locY: h-1
	
	(ary.get: locY*w+locX)=trailChar ?
	(
		quit: tru
	)|(
		ary.set: locY*w+locX, trailChar
		
		
		# display board
		
		i: 0 | i<6 | i: i+1 @
			print
		
		printc: 32
		
		x: 0 | x<w | x: x+1 @
			printc: 45
		
		print
		
		y: 0 |y<h |y: y+1 @
		(
			printc: 124
			
			x: 0 | x<w | x: x+1 @
			(
				x=locX && y=locY ?
					printc: meChar
				|
					printc: ary.get: y*w+x
			)
			
			printc: 124
			print
		)
		
		printc: 32
		
		x: 0 | x<w | x: x+1 @
			printc: 45
		3
		
		print
		
		# get input
		
		valid: fls
		
		!valid @
		(
			dir: inputInt
			valid: tru
			
			dir=3 ?
				locY: locY+1
			| dir=4 ?
				locX: locX+1
			| dir=2 ?
				locY: locY-1
			| dir=1 ?
				locX: locX-1
			|
				valid: fls
		)
	)
)

print
printc: 88
printc: 32
printc: 88
print
printc: 45
printc: 45
printc: 45
print
